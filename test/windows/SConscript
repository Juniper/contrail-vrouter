#
# Copyright (c) 2018 Juniper Networks, Inc. All rights reserved.
#

Import('VRouterEnv')
env = VRouterEnv.Clone()

env.Append(CPPPATH = [env['TOP_INCLUDE'], 'include'])

# Treat warnings as errors
env.Append(CPPFLAGS = '/WX')

env.Replace(LIBPATH = env['TOP_LIB'])
env.Append(LIBPATH = ['.', '../../windows'])

# vRouter code
env.Replace(LIBS = ['win_packet'])
# 3rd party libs
env.Append(LIBS = 'cmocka')

env.AppendENVPath('Path', Dir('#build/bin'))

common_tests_src = ['fake_win_packet_raw.c']
common_tests_obj = env.Object(common_tests_src)

win_packet_clone_tests_src = ['test_win_packet_clone.c'] + common_tests_obj
win_packet_clone_tests = env.UnitTest('win_packet_clone_tests', win_packet_clone_tests_src)

win_packet_free_tests_src = ['test_win_packet_free.c'] + common_tests_obj
win_packet_free_tests = env.UnitTest('win_packet_free_tests', win_packet_free_tests_src)

test_suites = [
    win_packet_clone_tests,
    win_packet_free_tests,
]

kernel_tests = env.TestSuite('kernel-tests', test_suites)
env.Requires(kernel_tests, '#build/bin/cmocka.dll')
