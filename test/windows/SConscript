#
# Copyright (c) 2018 Juniper Networks, Inc. All rights reserved.
#

Import('VRouterEnv')
env = VRouterEnv.Clone()

env.Append(CPPPATH = [env['TOP_INCLUDE'], 'include'])

# Treat warnings as errors
env.Append(CPPFLAGS = '/WX')

env.Replace(LIBPATH = env['TOP_LIB'])
env.Append(LIBPATH = ['.', '../../windows'])

# vRouter code
env.Replace(LIBS = ['win_packet'])
# 3rd party libs
env.Append(LIBS = 'cmocka')

env.AppendENVPath('Path', Dir('#build/bin'))

common_tests_src = ['fake_win_packet_raw.c', 'fake_win_memory.c', 'fake_vrouter.c']
common_tests_obj = env.Object(common_tests_src)

test_suite_base_names = [
    'win_packet_clone',
    'win_packet_free',
    'win_pclone',
    'win_pfree',
    'win_packet_split_multi_packet',
]

test_suites = []
for test_suite_name in test_suite_base_names:
    test_suite_src = ['test_{}.c'.format(test_suite_name)] + common_tests_obj
    test_suite = env.UnitTest('{}_tests'.format(test_suite_name), test_suite_src)
    test_suites.append(test_suite)

kernel_tests = env.TestSuite('kernel-tests', test_suites)
env.Requires(kernel_tests, '#build/bin/cmocka.dll')
